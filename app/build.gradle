plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def keystorePropertiesFile = project.rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'com.marastro.mykkumi.android'
    compileSdk compile_sdk_version

    defaultConfig {
        applicationId "com.marastro.mykkumi.android"
        minSdk min_sdk_version
        targetSdk target_sdk_version

        versionCode 3
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            // Firebase Crashlytics 활성화
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                mappingFileUploadEnabled true
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    appName : "@string/app_name",
                    appIcon: "@drawable/ic_mykkumi_logo"
            ]
            signingConfig signingConfigs.release
        }
        debug {
            // Firebase Crashlytics 비활성화
            firebaseCrashlytics {
                nativeSymbolUploadEnabled false
                mappingFileUploadEnabled false
            }

            applicationIdSuffix '.dev'
            manifestPlaceholders = [
                    appName : "@string/app_name_dev",
                    appIcon: "@drawable/ic_mykkumi_logo_dev"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = jvm_target_version
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    // feature
    implementation project(":feature:home")
    implementation project(":feature:around")
    implementation project(":feature:shopping")
    implementation project(":feature:mypage")
    implementation project(":feature:auth")
    implementation project(":feature:post")

    // data
    implementation project(":core:data")

    // domain
    implementation project(":core:domain")

    implementation project(":core:common-ui")
    implementation project(":core:analytics")

    // UI - Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Test
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    implementation "com.google.firebase:firebase-analytics"
    releaseImplementation "com.google.firebase:firebase-crashlytics"
}